plugins {
    id 'java'
    id 'java-library'
    id 'org.jetbrains.kotlin.jvm' version '1.4.10'
    id 'jacoco'
    id 'org.barfuin.gradle.jacocolog' version '1.2.3'
}

group 'pcf.crskdev'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    jcenter()
}

configurations {
    ktlint
}

test {
    useJUnitPlatform()
    testLogging {
        events "PASSED", "FAILED", "SKIPPED", "STANDARD_OUT", "STANDARD_ERROR"
    }
    finalizedBy jacocoTestReport, jacocoTestCoverageVerification
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
    reports {
        xml.enabled true
        csv.enabled false
        html.destination file("${buildDir}/jacocoHtml")
    }
    afterEvaluate {
        getClassDirectories().setFrom(classDirectories.files.collect {
            fileTree(dir: it).filter ({file -> !file.name.contains('MainKt')})
        })
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.8
            }
        }

        rule {
            enabled = false
            element = 'CLASS'

            limit {
                counter = 'LINE'
                value = 'TOTALCOUNT'
                maximum = 0.3
            }
        }
    }
}

jacoco {
    toolVersion = "0.8.5"
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"

    ktlint "com.pinterest:ktlint:0.39.0"

    def kotest = "4.2.5"
    testImplementation "io.kotest:kotest-runner-junit5:${kotest}" // for kotest framework
    testImplementation "io.kotest:kotest-assertions-core:${kotest}" // for kotest core jvm assertions
    testImplementation "io.kotest:kotest-property:${kotest}" // for kotest property test
    testImplementation "io.mockk:mockk:1.10.6" // for mockk
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs = ["-Xallow-result-return-type"]
    }
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
    // to generate report in checkstyle format prepend following args:
    // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // see https://github.com/pinterest/ktlint#usage for more
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}